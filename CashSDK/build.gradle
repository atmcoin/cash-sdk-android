apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply from: rootProject.file("jacoco.gradle")

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    testOptions {

        unitTests.all {
            useJUnit {
                if (project.hasProperty('integrationTest') && integrationTest == "true") {
                    includeCategories 'cash.just.sdk.IntegrationTest'

                } else {
                    excludeCategories 'cash.just.sdk.IntegrationTest'
                }
            }

            test {
                if (project.hasProperty('integrationTest') && integrationTest == "true") {

                    //Get last path of default set path.
                    def junitXmlTestTaskName = file("$reports.junitXml.destination").name
                    def htmlTestTaskName = file("$reports.html.destination").name

                    reports.junitXml.destination = file("$project.buildDir/reports/integrationTest/$junitXmlTestTaskName")
                    reports.html.destination = file("$project.buildDir/reports/integrationTest/$htmlTestTaskName")

                }

            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable = true
            jniDebuggable = true
            minifyEnabled = false
            testCoverageEnabled true
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // Square
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.squareup.moshi:moshi:1.9.2'
    implementation("com.squareup.moshi:moshi-kotlin:1.11.0")
    implementation 'com.squareup.retrofit2:converter-moshi:2.7.2'
    api 'com.squareup.retrofit2:retrofit:2.7.2'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.9.2'

    implementation 'com.google.code.gson:gson:2.8.6'

    testImplementation('com.github.tomakehurst:wiremock-jre8:2.26.3')
}
